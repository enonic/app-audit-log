plugins {
    id 'maven-publish'
    id "com.enonic.defaults" version "2.0.1"
    id "com.enonic.xp.app" version "2.0.0"
    id "com.github.node-gradle.node" version "3.0.0"
}

app {
    name = "${appName}"
    displayName = "${appDisplayName}"
    vendorName = "${vendorName}"
    vendorUrl = "${vendorUrl}"
    systemVersion = "${xpVersion}"
}

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-auditlog:${xpVersion}"
    include "com.enonic.xp:lib-node:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-admin:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"

    include "com.enonic.lib:lib-thymeleaf:2.0.0"
    include "com.enonic.lib:lib-admin-ui:3.0.2"

    include 'com.enonic.lib:lib-license:3.0.0'
}

// Setup task in dev and not devmode? for getting not minified files.
task materialcssStyles(type: Copy) {
    from "node_modules/@materializecss/materialize/dist/css"
    include "*.min.css"
    into "$buildDir/resources/main/assets/styles"
}

task materialcssJs (type: Copy) {
    from "node_modules/@materializecss/materialize/dist/js"
    include "*.js"
    into "$buildDir/resources/main/assets/js"
}

task buildSass(type:Exec) {
    // See the sass.js for how the sass is compiled
    commandLine "node", "build_scripts/sass.js"
    inputs.files("src/main/resources/assets/styles")
    outputs.file("$buildDir/resources/main/assets/styles/styles.min.css")

    // Clean sometimes picks this up? Debug mostly
    doLast {
        assert file("$buildDir/resources/main/assets/styles/styles.min.css").exists()
    }
}

node {
    version = "14.12.0"
    download = true
}

task buildJS(type: NpmTask) {
    args = ["run", "build"]
    inputs.files("src/main/resources/assets/js")
    outputs.file("$buildDir/resources/main/assets/js/main.js")

    doLast {
        assert file("$buildDir/resources/main/assets/js/main.js")
    }
}

sourceSets {
	main {
		resources {
            exclude "**/*.scss"
            exclude "**/*.es6"
		}
	}	
}

jar {
    includeEmptyDirs = false
    dependsOn += buildJS
    dependsOn += buildSass
    dependsOn += materialcssStyles
    dependsOn += materialcssJs
} 

repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo()
}